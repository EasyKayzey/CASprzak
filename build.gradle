plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    implementation 'org.jetbrains:annotations:16.0.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_14
}

application {
    mainClassName = 'ui.CASUI'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['tests']
        }
    }
}

run {
    standardInput = System.in

    // https://stackoverflow.com/questions/55433883/how-to-enable-java-12-preview-features-with-gradle
    jvmArgs(['--enable-preview'])
}

jar {
    manifest {
        attributes 'Main-Class': 'ui.CASUI'
    }
}

test {
    useJUnitPlatform()

    // https://stackoverflow.com/questions/55433883/how-to-enable-java-12-preview-features-with-gradle
    jvmArgs(['--enable-preview'])
}

// https://stackoverflow.com/questions/43937169/gradle-application-plugin-with-multiple-main-classes/46938169
task(debugger, dependsOn: 'classes', type: JavaExec) {
    main = 'ui.CASDebugger'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in

    // https://stackoverflow.com/questions/55433883/how-to-enable-java-12-preview-features-with-gradle
    jvmArgs(['--enable-preview'])
}

// https://stackoverflow.com/questions/55433883/how-to-enable-java-12-preview-features-with-gradle
tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}
